#!/usr/bin/perl

# -- CHROOTX, written by Rene K. Mueller <spiritdude@gmail.com>
#       toolset for handling chrooted systems, 
#       http://github.com/Spiritdude/ChrootX/
#
my $APPNAME = 'chrootx';
my $VERSION = '0.0.7';
#
# License: GPLv3
#
# Description:
#   Chrootx is for handling: create, clone, delete, start and stop chrooted systems, identified with 
#      an ID optionally jchroot is used (if it exists) to improve isolation further.
#   It's mainly targeted to simplify creating pseudo virtualization, e.g. if you run within a VM already,
#      like a virtual server where lxc or qemu-kvm won't run due lack of functionality, chrootx allows to 
#      further create sub systems without any hassle.
#
# History:
# 2014/12/08: 0.0.6: support of file-based filesystem using qemu-img
# 2014/12/08: 0.0.5: clone/copy added
# 2014/12/06: 0.0.4: more details of list/info
# 2014/12/04: 0.0.3: /var/lib/chrootx/fstab as default mount for all chroot, proper stop of [j]chroot
# 2014/12/02: 0.0.2: jchroot included, which allows to set hostname and sub-process isolation
# 2014/11/30: 0.0.1: first version

use POSIX;
use JSON;
use DateTime;
use Time::HiRes qw(time);
use strict;

my $conf;

# -- defaults
$conf->{root} = '/var/lib/chrootx';
$conf->{chroot} = '/usr/sbin/chroot';
$conf->{jchroot} = '/usr/sbin/jchroot';
$conf->{size} = '4G';
$conf->{format} = 'qcow2';
$conf->{fs} = 'ext4';

conf("/etc/chrootx.conf") if(-e "/etc/chrootx.conf");

my(@mnts);              # -- fstab for chroots

my(%_mounted,$_pid);    # -- catched data

my(%ff) = ('raw'=>1, 'cloop'=>2, 'cow'=>3, 'qcow'=>4, 'qcow2'=>5, 'vmdk'=>6, 'vdi'=>7);

# -- fetch default mounts in chroot
open(F,"$conf->{root}/fstab");
while(<F>) {
   chop;
   next if(/^\s*#/);
   push(@mnts,$_);
}
close(F);

while($ARGV[0]=~/^-/) {
   $_ = shift(@ARGV);
   if(/^-conf/) {
      conf(shift(@ARGV));
   }
   help() if(/^-help$/||/^-h/);
   if(/^-(\w+)=(.*)$/||/^--(\w+)=(.*)$/) {
      $conf->{$1} = $2;

   } elsif(/^-(\w+)/||/^--(\w+)/) {
      my $v = $1;
      if($v eq 'version') {
         print "$APPNAME $VERSION\n"; 
         exit 0; 
      }
      foreach (split(/|/,$v)) {
         $_ = 'verbose' if($_ eq 'v');
         $conf->{$_}++;
      }
      next;
   }
   deepVar($conf,$1,$2), next if(/^--?([\.\w]+)=(.*)/);
}

my $cmd = shift(@ARGV);

if($> != 0) {
   print STDERR "ERROR: $APPNAME must be run as root!\n";
   exit -1;
}

mkdir $conf->{root} unless(-e $conf->{root});
mkdir "$conf->{root}/roots" unless(-e "$conf->{root}/roots");
mkdir "$conf->{root}/imgs" unless(-e "$conf->{root}/imgs");

delete $conf->{jchroot} unless(-e $conf->{jchroot});

sub help {

   opendir(D,"$conf->{root}/templates");
   my(@t) = sort grep(!/^\./,readdir(D));
   closedir(D);

   my $types = join(', ',map { $_ = "'$_'" } @t);
   my $ftypes = join(', ',map { $_ = "'$_'" } sort keys %ff);
   
   print "$APPNAME $VERSION usage: [<options>] <command> [<arguments>]
   options:
      -verbose or -v or -vv   increase verbosity
      -conf <file>            consider configuration file (default: /etc/chrootx.conf)
      -version or --version   display version
      -l                      long output
      -i                      image-based root 
      --format=<type>         image format (default: $conf->{format})
                                 type: $ftypes
      --size=<size>           size of image (default: $conf->{size})
                                 e.g. '200M', '2G' etc
      --fs=<type>             fs-type in the image (default: $conf->{fs})
      
   commands:
      create <id> [<type>]    aka 'new', 'install', (default type: 'minimal')
                                 type: $types
      clone <src> <dst>       clone existing chroot, same as 'new <src> clone:<dst>'
      delete <id>             aka 'deinstall', 'remove', 'destroy'
      start <id>              aka 'run'
      stop <id>               aka 'halt', 'kill', 'abort'
      info <id>               aka 'status'
      list                    default action, use -l to list details

   examples:
      sudo chrootx new sys01 
      sudo chrootx new sys02 ubuntu
      sudo chrootx -i new sys03 clone:sys02
      sudo chrootx -i --size=60G new sys04 clone:sys02
      sudo chrootx clone sys02 sys05
      sudo chrootx start sys05 /etc/init.d/lighttpd start
      
";   
   exit 0;
}

print "$APPNAME: performed <$cmd>\n" if($conf->{verbose});

if($cmd eq 'create'||$cmd eq 'new'||$cmd eq 'install') {
   help() unless($#ARGV>=0);
   my($id) = sane(shift(@ARGV),'id');
   my $type = 'minimal';

   $type = shift(@ARGV) if($#ARGV>=0);

   if(!existRoot($id)) {
      createRoot($id,$type);
   } else {
      print "<$id> exists already\n";
   }

} elsif($cmd eq 'delete'||$cmd eq 'remove'||$cmd eq 'deinstall'||$cmd eq 'destroy') {
   help() unless($#ARGV>=0);
   my($id) = sane(shift(@ARGV),'id');

   if(existRoot($id,{must=>1})) {
      if(statusRoot($id)eq'stopped') {
         umountRootImage($id);
         `rm -rf $conf->{root}/roots/$id`;
         foreach (keys %ff) {
            unlink("$conf->{root}/imgs/$id.$_");
         }
      } else {
         print "<$id> is still running, cannot delete it\n";
      }
   }
   
} elsif($cmd eq 'start'||$cmd eq 'run') {
   help() unless($#ARGV>=0);
   my($id) = sane(shift(@ARGV),'id');

   push(@ARGV,"/bin/sh") if($#ARGV<0);

   existRoot($id,{must=>1});

   my($i) = infoRoot($id);
   $i->{stime} = time();

   mountRootImage($id);
   
   writeJSON("$conf->{root}/roots/$id/.info.json",$i);
   
   if($conf->{jchroot}) {
      foreach (@mnts) {
         my(@d) = split(/\s+/);
         mkrdir("$conf->{root}/roots/$id$d[1]") unless(-e "$conf->{root}/roots/$id$d[1]");
      }
      exec($conf->{jchroot},"-f","$conf->{root}/fstab","-n",$id,"$conf->{root}/roots/$id",@ARGV);

   } else {
      mountRoot($id);
      exec("/usr/sbin/chroot","$conf->{root}/roots/$id",@ARGV);
   }
   
} elsif($cmd eq 'stop'||$cmd eq 'kill'||$cmd eq 'abort'||$cmd eq 'halt') {
   help() unless($#ARGV>=0);
   my($id) = sane(shift(@ARGV),'id');

   existRoot($id,{must=>1});
   if(statusRoot($id)eq'running') {
      foreach (@{$_pid->{$id}->{pid}}) {
         kill 'KILL', $_;
      }
   } else {
      print "<$id> is not running\n";
   }   
   if($conf->{jchroot}) {
      ;
   } else {
      umountRoot($id);
   }
   
} elsif($cmd eq 'mv'||$cmd eq 'rename') {
   help() unless($#ARGV>=1);
   my($id1) = sane(shift(@ARGV),'id');
   my($id2) = sane(shift(@ARGV),'id');

   existRoot($id1,{must=>1});

   if(statusRoot($id1)eq'stopped') {
      existRoot($id2,{mustNot=>1});
      rename("$conf->{root}/roots/$id1","$conf->{root}/roots/$id2");
      foreach (keys %ff) {
         rename("$conf->{root}/imgs/$id1.$_","$conf->{root}/imgs/$id2.$_");
      }

   } else {
      print "<$id1> is still running, please stop before rename\n";
   }

} elsif($cmd eq 'clone'||$cmd eq 'copy') {
   help() unless($#ARGV>=1);
   my($id1) = sane(shift(@ARGV),'id');
   my($id2) = sane(shift(@ARGV),'id');

   existRoot($id1,{must=>1});

   if(statusRoot($id1)eq'stopped') {
      existRoot($id2,{mustNot=>1});

      createRoot($id2,"clone:$id1");

   } else {
      print "<$id1> is still running, please stop before clone/copy\n";
   }
   
} elsif($cmd eq 'info'||$cmd eq 'status') {
   help() unless($#ARGV>=0);
   my($id) = sane(shift(@ARGV),'id');
   $conf->{l}++;
   existRoot($id,{must=>1});
   dispRoot($id);

} elsif($cmd eq 'help') {
   help();

} elsif($cmd eq 'list'||!$cmd) {
   opendir(D,"$conf->{root}/roots");
   my(@c) = grep((!/\./)&&-d "$conf->{root}/roots/$_",readdir(D));
   closedir(D);

   my $n = 0;
   my $s;

   $s->{running} = $s->{stopped} = 0;

   foreach my $c (sort @c) {
      my $i = dispRoot($c);
      $s->{$i->{status}}++;
      $s->{size} += $i->{size};
      $n++;
   }

   if($n==0) {
      print "\tno chroot yet created\n";
   } else {
      print "total $n chroot",($n>1?"s":"")," defined, running: $s->{running}, stopped: $s->{stopped}, disk usage: ",prettyNumber($s->{size})," bytes\n";
   }

} else {
   help();
}

# ------------------------------------------------------------------------------------------------------------------------

sub createRoot {
   my($id,$type) = @_;
   my $i;

   unless(-e "$conf->{root}/roots/$id") {
      mkdir "$conf->{root}/roots/$id";
      $i->{ctime} = time();
      $i->{type} = $type if($type);

      if($conf->{i}) {
         $i->{format} = $conf->{format};
         print "\tcreate image ($conf->{format}, $conf->{size})\n";
         `qemu-img create -f $conf->{format} $conf->{root}/imgs/$id.$conf->{format} $conf->{size}`;
         mountRootImage($id);
      }
      
      writeJSON("$conf->{root}/roots/$id/.info.json",$i);

      if(-e "$conf->{root}/templates/$type") {
         if(1) {
            if(fork()==0) {
               print "\tapply template $type (@ARGV)\n";
               exec("$conf->{root}/templates/$type","$conf->{root}/roots/$id",@ARGV);
            } else {
               wait();
            }
         } else {
            `$conf->{root}/templates/$type $conf->{root}/roots/$id`;
         }

      } elsif($type=~/^clone:(\S+)/) {
         my $ido = $1;
         print "\tcloning $ido\n";
         `cd $conf->{root}/roots/$ido && tar cf - * | (cd $conf->{root}/roots/$id; tar xf -)`;
         if(0) {
            my $io = infoRoot($ido);
            $i->{type} = $io->{type};
         }
         
      } else {
         print "\tERROR: <$type> template not found\n";
         exit 1;
      }
      $i->{size} = sizeRoot($id);
      writeJSON("$conf->{root}/roots/$id/.info.json",$i);

   } else {
      print "\t<$id> already exists, skipped\n";
   }
}

sub existRoot {
   my($id,$opts) = @_;
   if($opts->{mustNot}&&-e "$conf->{root}/roots/$id") {
      print "ERROR: <$id> does exist (already)\n";
      exit -1;
   }
   return 1 if(-e "$conf->{root}/roots/$id");
   if($opts->{must}) {
      print "ERROR: <$id> doesn't exist\n";
      exit -1;
   }
}

sub statusRoot {
   my($id) = @_;
   
   if(!$_pid) {
      my %pr;
      
      my(@p) = split(/\n/,`ps -efw`);       # -- fetch all processes

      shift(@p);           # -- drop 1st line

      foreach (@p) {
         my(@d) = split(/\s+/);

         # print "odd: $d[1] has already a parent: $pr{$d[1]}\n" if(defined $pr{$d[1]});
         $pr{$d[1]} = $d[2];

         if(0) {
            if(index($_,$conf->{jchroot})>=0&&/ -n (\S+)/) {
               $_pid->{$1}->{pid} = $d[1];
               $_pid->{$1}->{pids}++;
               # print "$1: $d[1]\n";
   
            } elsif(index($_,$conf->{chroot})>=0&&index($_,$conf->{root})>=0) {
               my $n = index($_,$conf->{root});
               my $s = substr($_,$n);
               $_pid->{$1}->{pid} = $d[1] if($s =~ /^(\S+)/);
   
            } elsif(/[\/ ]chrootx start (\S+)$/) {
               $_pid->{$1}->{pid} = $d[1];
            }

         } elsif(0) {
            open(F,"/proc/$d[1]/mounts"); my(@m) = <F>; close(F);
            foreach (@m) {
               my $p = index($_,"$conf->{root}/roots/");
               my $t = substr($_,$p+length("$conf->{root}/roots/"));
               if($p>=0&&$t=~/([^\/]+)/) {
                  push(@{$_pid->{$1}->{pid}},$d[1]);
                  $_pid->{$1}->{pids}++;
                  last;
               }
            }
         } else {    # -- simplest way so far, independent if jchroot or chroot
            $_ = readlink("/proc/$d[1]/exe");
            my $p = index($_,"$conf->{root}/roots/");
            my $t = substr($_,$p+length("$conf->{root}/roots/"));
            if($p>=0&&$t=~/([^\/]+)/) {
               push(@{$_pid->{$1}->{pid}},$d[1]);
               $_pid->{$1}->{pids}++;
               
            }
         }
      }

      foreach (@p) {                      # -- check all processes again, determine which process belongs to which chroot
         my(@d) = split(/\s+/);

         # print "check $px ";
         foreach my $id (keys %{$_pid}) {
            my $done;
            my(@pxs);
            my $px = $d[1];

            push(@pxs,$px);
            while(defined $pr{$px}) {     # -- determine whether origin is chroot
               $px = $pr{$px};
               push(@pxs,$px);

               # print " -> $px";
               if($px==$_pid->{$id}->{pid}) {
                  $_pid->{$id}->{pids}++;
                  # print ", belongs to $id ($_pid->{$id}->{pid})\n";
                  # print "$id: @pxs: $_\n";
                  $done++;
                  last;
               } 
            } 
            last if($done);
         }
         # print "\n";
      }
   }
   return $_pid->{$id}->{pid}?"running":"stopped";
   return isMounted("$conf->{root}/roots/$id/dev/pts")?"running":"stopped";
}

sub isMounted {
   my($m) = @_;

   if(!%_mounted) {
      foreach (split(/\n/,`mount`)) {
         my(@d) = split(/\s+/);
         $_mounted{$d[2]}++;
      }
   }
   print "$m is ",($_mounted{$m}?"mounted":"not mounted"),"\n" if($conf->{verbose});
   return $_mounted{$m};
}

sub mountRoot {
   my($id) = @_;

   if(!isMounted("$conf->{root}/roots/$id/dev/pts")) {
      print "mount special dirs\n" if($conf->{verbose});
      foreach (@mnts) {
         my(@d) = split(/\s+/);
         #   proc     /proc  proc    defaults                  0  0
         #   sysfs    /sysfs   sysfs   defaults                  0  0
         #   #/home    /home  none    bind,rw                   0  0
         #   /dev/pts /dev/pts none  bind,rw                   0  0
         #   #/var/run /var/run tmpfs rw,nosuid,noexec,mode=755 0  0
         #   #/etc/resolv.conf /etc/resolv.conf none bind,ro    0  0
         mkrdir("$conf->{root}/roots/$id$d[1]") unless(-e "$conf->{root}/roots/$id$d[1]");
         print "mounting $d[0] -> $conf->{root}/roots/$id$d[1]\n" if($conf->{verbose});
         `mount -o $d[3] -t $d[2] $d[0] $conf->{root}/roots/$id$d[1]`; # unless(-e "$conf->{root}/roots/$id$d[1]");
      }
   }
}

sub umountRoot {
   my($id) = @_;
   
   if(isMounted("$conf->{root}/roots/$id/dev/pts")) {
      foreach (@mnts) {
         my(@d) = split(/\s+/);
         `umount -f $conf->{root}/roots/$id$d[1]`;
      }
   }
}

sub mountRootImage {
   my($id) = @_;
   `modprobe nbd 2> /dev/null`;
   foreach (keys %ff) {
      if(-e "$conf->{root}/imgs/$id.$_") {
         print "$id is file-based filesystem ($_)\n" if($conf->{verbose});
         mountNBD("$conf->{root}/imgs/$id.$_","$conf->{root}/roots/$id");
         last;
      }
   }
}

sub umountRootImage {
   my($id) = @_;
   foreach (split(/\n/,`mount`)) {
      my(@d) = split(/\s+/);
      if($d[2] eq "$conf->{root}/roots/$id") {
         `umount -f $conf->{root}/roots/$id`;
         my($d) = ($d[0]=~/nbd(\d+)/);
         `qemu-nbd -d /dev/nbd$d`;
         last;
      }
   }
}

sub mountNBD {
   my($dev,$mnt) = @_;
   my $d = 0;
   my $mnted;
   foreach (split(/\n/,`mount`)) {
      my(@d) = split(/\s+/);
      if($d[2] eq $mnt) {
         $mnted++;
      }
      if(/\/nbd(\d+)/) {
         $d = $1+1 if($1>=$d);         # -- get next free nbd id
      }
   }
   unless($mnted) {
      print "mounting $dev to $mnt via /dev/nbd$d\n" if($conf->{verbose});
      `qemu-nbd -c /dev/nbd$d $dev`;

      unless(-e "/dev/nbd${d}p1") {
         print "create single partition and mkfs ext4 on $dev\n" if($conf->{verbose});

         print "\tpartitioning image\n";
         `echo "o\nn\np\n1\n\n\nw\nq" | fdisk /dev/nbd$d 2> /dev/null`;    # -- create single partition

         print "\tcreating fs ($conf->{fs})\n";
         `mkfs -t $conf->{fs} /dev/nbd${d}p1 2> /dev/null`;                          # -- make filesystem
      }
      `mount /dev/nbd${d}p1 $mnt`;
   }
}
   
sub dispRoot {
   my($id) = @_;
   my $i = infoRoot($id);
   
   if($conf->{l}) {
      print "   $id:\n";
      foreach (qw(status processes type ctime stime uptime size)) {
         next if(!defined $i->{$_});
         my $v = $i->{$_};
         if(/uptime/||$conf->{types}->{$_}eq'time') {
            $v = prettyTime($v);
         } elsif(/time/||$conf->{types}->{$_}eq'date') {
            $v = prettyDate($v)." (".prettyAgo($i->{$_}).")" 
         } else {
            $v = prettyNumber($v) if($v=~/^[1-9]\d*$/);
         }
         if(1) {
            print sprintf("%15s: $v",$_);
         } else {
            print "\t$_: $v";
         }
         print " ".$conf->{units}->{$_} if($conf->{units}->{$_});
         print "\n";
      }
      print toJSON($i),"\n" if($conf->{verbose});
      print "\n";
      
   } else {
      my $p; $p = ": ".$i->{processes}." process".($i->{processes}>1?"es":"") if($i->{status}eq'running');
      print sprintf("%20s: $i->{status}$p\n","$id ($i->{type})");
   }
   return $i;
}

sub infoRoot {
   my($id) = @_;
   my($i) = readJSON("$conf->{root}/roots/$id/.info.json");
   my $p = "$conf->{root}/roots/$id";

   #if($conf->{l}) {
      unless($i->{size}) {
         $i->{size} = sizeRoot($id);
      }
      unless($i->{ctime}) {
         $i->{ctime} = (stat("$conf->{root}/roots/$id"))[10];
      }
   #}
   unless($i->{type}) {
      $i->{type} = 'custom';
   }

   $i->{status} = statusRoot($id);

   if($i->{status}eq'running') {
      $i->{processes} = $_pid->{$id}->{pids};
      $i->{uptime} = time()-$i->{stime};
   } else {
      foreach(qw(processes uptime stime)) {
         delete $i->{$_};
      }
   }
   
   return $i;
}

sub sizeRoot {
   my($id) = @_;
   return $1*1024 if(`du -sk '$conf->{root}/roots/$id'`=~/(\d+)/);
   return 0;
}

sub conf {
   my($f) = @_;
   my $c = readJSON($f);
   foreach (keys %$c) {       # -- merge it ...
      $conf->{$_} = $c->{$_};
   }
}

sub sane {
   my($i,$t) = @_;
   if($t eq 'id') {
      $i =~ s/[^a-zA-Z0-9_]//g;
      if(length($i)==0) {
         print "ERROR: <$i> not a valid identifier\n";
         exit 1;
      }

   } else {
      $i =~ s/\s//g;
   }
   return $i;
}

# -----------------------------------------------------------------------------------------------------------------

sub fromJSON {
   # -- eval so failed decode doesn't die process
   return eval { JSON->new->allow_nonref->relaxed([1])->decode($_[0]) };
}

sub toJSON {
   my($v,$a) = @_;
   my $j = JSON->new->allow_nonref->allow_blessed;
   $j = $j->pretty unless($a&&!$a->{pretty});
   return $j->encode($_[0]);
}

sub writeJSON {
   my($n,$v,$a) = @_;
   my $fh;
   open($fh,'>',"$n.new");
   print $fh toJSON($v,$a);
   close($fh);
   rename("$n.new",$n);
}

sub readJSON {
   my($n) = @_;
   local $/;
   my $fh;
   my $v;
   open($fh,'<',$n);
   $v = <$fh>;
   close($fh);
   return fromJSON($v);
}

sub deepVar {
   my($r,$k,$v) = @_;
   my $p = $r;
   
   my(@pp) = split(/\./,$k);
   $k = pop(@pp);
   foreach (@pp) {
      $p->{$_} = {} unless(defined $p->{$_});
      $p = $p->{$_};
   }
   if($#_>=2) {      # -- writing
      $p->{$k} = $v;
   } else {          # -- reading
      return $p->{$k};
   }
}

sub mkrdir {
   my($p) = @_;
   my $r;
   foreach (split(/\//,$p)) {
      next if(length($_)==0);
      $r .= "/$_";
      mkdir $r unless(-e $r);
   }
}

sub prettyDate {
   my($t) = @_;
   @_ = localtime($t);
   #sprintf("%s/%02d/%02d %02d:%02d:%02d.%03d",$_[5]<0?sprintf("%04d",$_[5]*-1+1)."BCE":sprintf("%04d",$_[5]),$_[4]+1,$_[3],$_[2],$_[1],$_[0],($t-int($t))*1000);
   sprintf("%04d/%02d/%02d %02d:%02d:%02d.%03d",$_[5]+1900,$_[4]+1,$_[3],$_[2],$_[1],$_[0],($t-int($t))*1000);
}

sub prettyAgo {
   my $t = time()-$_[0];
   my $opts = $_[1];
   my $ago = $t>=0;
   my $to = '';
   
   $t = $ago?$t:-$t;

   my $p = 0; my $m = 1;
   my @out;
   my(@tx) = _localtime($t);
   
   $tx[5] -= 1970;      # -- convert to relative time 1970 => 0
   $tx[3] -= 1;         # 1st day => day 0
   $tx[6] = int($tx[5]/1000); $tx[5] %= 1000;

   my(@si) = (qw(sec min hr day month yr mnium));
   my(@pl) = (qw(secs mins hrs days months yrs mnia));
   foreach my $f (@si) {
      push(@out,$tx[$p].($tx[$p]>1?$pl[$p]:$f));
      $m = $p+1 if($p&&$tx[$p]>0);
      $p++;
   }
   @out = splice(@out,0,$m) if($m<$#out+1);
   @out = reverse @out;
   $to = join(" ",@out);

   return $to.($ago?" ago":" ahead");
}

sub prettyNumber {
   my($d,$opts) = @_;
   my($n,$o);
   $o = '';
   do {
      $o = ",$o" if($n&&$n%3==0);
      $o = ($d%10).$o;
      $d = int($d/10);
      $n++;
   } while($d>0);
   return $o;
}

sub _localtime {     
   my($sec) = @_;
   my $dt = DateTime->from_epoch(epoch=>$sec);
   return ($dt->second,$dt->minute,$dt->hour,$dt->day,$dt->month-1,$dt->year);
}

sub prettyTime {
   my($t,$opts) = @_;

   $t = sprintf("%.f",$t) if($t=~/e/);    # -- 1e-9 => 0.000000001
   
   my $to = '';
   
   my $p = 0; my $m = 1;
   my @out;
   my(@tx) = _localtime($t);
   
   $tx[5] -= 1970;      # -- convert to relative time 1970 => 0
   $tx[3] -= 1;         # 1st day => day 0
   $tx[6] = int($tx[5]/1000); $tx[5] %= 1000;

   unshift(@tx,int(($t-int($t))*1000));
   unshift(@tx,int(($t-int($t))*1_000_000)%1000);
   
   my(@si) = (qw(us ms sec min hr day month yr mnium));
   my(@pl) = (qw(us ms secs mins hrs days months yrs mnia));
   foreach my $f (@si) {
      push(@out,$tx[$p].($tx[$p]>1?$pl[$p]:$f));
      $m = $p+1 if($p&&$tx[$p]>0);
      $p++;
   }
   @out = splice(@out,0,$m) if($m<$#out+1);
   @out = splice(@out,$#out-$opts->{precision}+1,$opts->{precision}) if($opts->{precision});
   @out = reverse @out;
   $to = join(" ",@out);

   return $to; #.sprintf(" (%.6f)",$t);
}

